OBJECT Report 496 Batch Post Purchase Orders
{
  OBJECT-PROPERTIES
  {
    Date=04/03/16;
    Time=16:51:03;
    Modified=Yes;
    Version List=NAVW19.00,TAH.002;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Batch Post Purchase Orders;
               FRA=TPL validation commandes achat];
    ProcessingOnly=Yes;
  }
  DATASET
  {
    { 4458;    ;DataItem;                    ;
               DataItemTable=Table38;
               DataItemTableView=SORTING(Document Type,No.)
                                 WHERE(Document Type=CONST(Order));
               ReqFilterHeadingML=[ENU=Purchase Order;
                                   FRA=Commande achat];
               OnPreDataItem=BEGIN
                               IF ReplacePostingDate AND (PostingDateReq = 0D) THEN
                                 ERROR(Text000);

                               CounterTotal := COUNT;
                               //AC BDU OPTI 1.00
                               IF GUIALLOWED THEN
                               //AC BDU OPTI 1.00
                               Window.OPEN(Text001);
                             END;

               OnAfterGetRecord=VAR
                                  ApprovalsMgmt@1000 : Codeunit 1535;
                                BEGIN
                                  IF ApprovalsMgmt.IsPurchaseApprovalsWorkflowEnabled("Purchase Header") OR (Status = Status::"Pending Approval") THEN
                                    CurrReport.SKIP;

                                  IF CalcInvDisc THEN
                                    CalculateInvoiceDiscount;

                                  Counter := Counter + 1;

                                  //AC BDU OPTI 1.00 BEGIN
                                  IF GUIALLOWED THEN BEGIN
                                  //AC BDU OPTI 1.00 BEGIN
                                    Window.UPDATE(1,"No.");
                                    Window.UPDATE(2,ROUND(Counter / CounterTotal * 10000,1));
                                  //AC BDU OPTI 1.00 BEGIN
                                  END;
                                  //AC BDU OPTI 1.00 END
                                  Receive := ReceiveReq;
                                  Invoice := InvReq;
                                  CLEAR(PurchPost);
                                  PurchPost.SetPostingDate(ReplacePostingDate,ReplaceDocumentDate,PostingDateReq);
                                  IF IsApprovedForPostingBatch THEN
                                    IF PurchPost.RUN("Purchase Header") THEN BEGIN
                                      CounterOK := CounterOK + 1;
                                      IF MARKEDONLY THEN
                                        MARK(FALSE);
                                    END;
                                END;

               OnPostDataItem=BEGIN
                                //AC BDU OPTI 1.00
                                IF GUIALLOWED THEN
                                //AC BDU OPTI 1.00
                                  Window.CLOSE;
                                //AC BDU OPTI 1.00
                                IF ShowEndMsg THEN
                                //AC BDU OPTI 1.00
                                  MESSAGE(Text002,CounterOK,CounterTotal);
                              END;

               ReqFilterFields=No.,Status }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
      OnOpenPage=BEGIN
                   InitValue;
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             FRA=Options] }

      { 1   ;2   ;Field     ;
                  Name=Receive;
                  CaptionML=[ENU=Receive;
                             FRA=R‚ceptionner];
                  SourceExpr=ReceiveReq }

      { 2   ;2   ;Field     ;
                  Name=Invoice;
                  CaptionML=[ENU=Invoice;
                             FRA=Facture];
                  SourceExpr=InvReq }

      { 3   ;2   ;Field     ;
                  Name=PostingDate;
                  CaptionML=[ENU=Posting Date;
                             FRA=Date comptabilisation];
                  SourceExpr=PostingDateReq }

      { 4   ;2   ;Field     ;
                  Name=ReplacePostingDate;
                  CaptionML=[ENU=Replace Posting Date;
                             FRA=Remplacer date comptabilisation];
                  SourceExpr=ReplacePostingDate;
                  OnValidate=BEGIN
                               IF ReplacePostingDate THEN
                                 MESSAGE(Text003);
                             END;
                              }

      { 5   ;2   ;Field     ;
                  Name=ReplaceDocumentDate;
                  CaptionML=[ENU=Replace Document Date;
                             FRA=Remplacer date document];
                  SourceExpr=ReplaceDocumentDate }

      { 7   ;2   ;Field     ;
                  Name=CalcInvDiscount;
                  CaptionML=[ENU=Calc. Inv. Discount;
                             FRA=Calculer remise facture];
                  SourceExpr=CalcInvDisc;
                  OnValidate=BEGIN
                               PurchSetup.GET;
                               PurchSetup.TESTFIELD("Calc. Inv. Discount",FALSE);
                             END;
                              }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Enter the posting date.;FRA=Entrez une date comptabilisation.';
      Text001@1001 : TextConst 'ENU=Posting orders  #1########## @2@@@@@@@@@@@@@;FRA=Validation des commandes #1########## @2@@@@@@@@@@@@@';
      Text002@1002 : TextConst 'ENU=%1 orders out of a total of %2 have now been posted.;FRA=%1 commande(s) sur %2 enregistr‚e(s).';
      PurchLine@1003 : Record 39;
      PurchSetup@1004 : Record 312;
      PurchCalcDisc@1005 : Codeunit 70;
      PurchPost@1006 : Codeunit 90;
      Window@1007 : Dialog;
      ReceiveReq@1008 : Boolean;
      InvReq@1009 : Boolean;
      PostingDateReq@1010 : Date;
      CounterTotal@1011 : Integer;
      Counter@1012 : Integer;
      CounterOK@1013 : Integer;
      ReplacePostingDate@1014 : Boolean;
      ReplaceDocumentDate@1015 : Boolean;
      CalcInvDisc@1016 : Boolean;
      Text003@1017 : TextConst 'ENU=The exchange rate associated with the new posting date on the purchase header will not apply to the purchase lines.;FRA=Le taux de change associ‚ … la nouvelle date de comptabilisation sur l''en-tˆte achat ne s''applique pas aux lignes achat.';
      ShowEndMsg@1000000000 : Boolean;

    LOCAL PROCEDURE CalculateInvoiceDiscount@1();
    BEGIN
      PurchLine.RESET;
      PurchLine.SETRANGE("Document Type","Purchase Header"."Document Type");
      PurchLine.SETRANGE("Document No.","Purchase Header"."No.");
      IF PurchLine.FINDFIRST THEN
        IF PurchCalcDisc.RUN(PurchLine) THEN BEGIN
          "Purchase Header".GET("Purchase Header"."Document Type","Purchase Header"."No.");
          COMMIT;
        END;
    END;

    PROCEDURE InitializeRequest@2(NewReceiveReq@1006 : Boolean;NewInvReq@1003 : Boolean;NewPostingDateReq@1001 : Date;NewReplacePostingDate@1002 : Boolean;NewReplaceDocumentDate@1004 : Boolean;NewCalcInvDisc@1005 : Boolean);
    BEGIN
      InitValue;
      ReceiveReq := NewReceiveReq;
      InvReq := NewInvReq;
      PostingDateReq := NewPostingDateReq;
      ReplacePostingDate := NewReplacePostingDate;
      ReplaceDocumentDate := NewReplaceDocumentDate;
      IF NewCalcInvDisc THEN
        PurchSetup.TESTFIELD("Calc. Inv. Discount",FALSE);
      CalcInvDisc := NewCalcInvDisc;
    END;

    LOCAL PROCEDURE InitValue@3();
    BEGIN
      PurchSetup.GET;
      CalcInvDisc := PurchSetup."Calc. Inv. Discount";
    END;

    PROCEDURE InitReport@1180250001(ForReceiveReq@1180250001 : Boolean;ForInvReq@1180250002 : Boolean;ForPostingDateReq@1180250003 : Date;ForReplacePostingDate@1180250004 : Boolean;ForReplaceDocumentDate@1180250005 : Boolean;ForCalcInvDisc@1180250006 : Boolean;ForShowEndMsg@1180250007 : Boolean);
    BEGIN
      //AC1.00 BEGIN
      InitValue;
      ReceiveReq := ForReceiveReq;
      InvReq := ForInvReq;
      PostingDateReq := ForPostingDateReq;
      ReplacePostingDate := ForReplacePostingDate;
      ReplaceDocumentDate := ForReplaceDocumentDate;
      CalcInvDisc := ForCalcInvDisc;
      IF ForCalcInvDisc THEN
        PurchSetup.TESTFIELD("Calc. Inv. Discount",FALSE);
      ShowEndMsg := ForShowEndMsg;
      //AC1.00 END
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

