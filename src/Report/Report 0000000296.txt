OBJECT Report 296 Batch Post Sales Orders
{
  OBJECT-PROPERTIES
  {
    Date=04/03/16;
    Time=16:56:37;
    Modified=Yes;
    Version List=NAVW19.00,TAH.002;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Batch Post Sales Orders;
               FRA=TPL valider commandes vente];
    ProcessingOnly=Yes;
  }
  DATASET
  {
    { 6640;    ;DataItem;                    ;
               DataItemTable=Table36;
               DataItemTableView=SORTING(Document Type,No.)
                                 WHERE(Document Type=CONST(Order));
               ReqFilterHeadingML=[ENU=Sales Order;
                                   FRA=Commande vente];
               OnPreDataItem=BEGIN
                               IF ReplacePostingDate AND (PostingDateReq = 0D) THEN
                                 ERROR(Text000);

                               CounterTotal := COUNT;

                               //AC BDU OPTI 1.00
                               IF GUIALLOWED THEN
                               //AC BDU OPTI 1.00
                                 Window.OPEN(Text001);
                             END;

               OnAfterGetRecord=VAR
                                  ApprovalsMgmt@1000 : Codeunit 1535;
                                BEGIN
                                  IF ApprovalsMgmt.IsSalesApprovalsWorkflowEnabled("Sales Header") OR (Status = Status::"Pending Approval") THEN
                                    CurrReport.SKIP;

                                  IF CalcInvDisc THEN
                                    CalculateInvoiceDiscount;

                                  Counter := Counter + 1;

                                  //AC BDU OPTI 1.00 BEGIN
                                  IF GUIALLOWED THEN BEGIN
                                  //AC BDU OPTI 1.00 END
                                    Window.UPDATE(1,"No.");
                                    Window.UPDATE(2,ROUND(Counter / CounterTotal * 10000,1));
                                  //AC BDU OPTI 1.00 END
                                  END;
                                  //AC BDU OPTI 1.00 END

                                  Ship := ShipReq;
                                  Invoice := InvReq;
                                  CLEAR(SalesPost);
                                  SalesPost.SetPostingDate(ReplacePostingDate,ReplaceDocumentDate,PostingDateReq);


                                  //AB6.TAH.002
                                  IF InclureExpedPartielle THEN
                                     SalesPost.SetPartielShipping;
                                  //AB6.TAH.002

                                  IF IsApprovedForPostingBatch THEN
                                    IF SalesPost.RUN("Sales Header") THEN BEGIN
                                      CounterOK := CounterOK + 1;
                                      IF MARKEDONLY THEN
                                        MARK(FALSE);
                                    END;
                                END;

               OnPostDataItem=BEGIN
                                //AC BDU OPTI 1.00 BEGIN
                                IF GUIALLOWED THEN
                                  Window.CLOSE;
                                IF ShowEndMsg THEN
                                //AC 1.00Window.CLOSE;
                                  MESSAGE(Text002,CounterOK,CounterTotal);
                              END;

               ReqFilterFields=No.,Status }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
      OnOpenPage=BEGIN
                   SalesSetup.GET;
                   CalcInvDisc := SalesSetup."Calc. Inv. Discount";
                   ReplacePostingDate := FALSE;
                   ReplaceDocumentDate := FALSE;
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             FRA=Options] }

      { 1   ;2   ;Field     ;
                  Name=Ship;
                  CaptionML=[ENU=Ship;
                             FRA=Exp‚dier];
                  SourceExpr=ShipReq }

      { 2   ;2   ;Field     ;
                  Name=Invoice;
                  CaptionML=[ENU=Invoice;
                             FRA=Facture];
                  SourceExpr=InvReq }

      { 3   ;2   ;Field     ;
                  Name=PostingDate;
                  CaptionML=[ENU=Posting Date;
                             FRA=Date comptabilisation];
                  SourceExpr=PostingDateReq }

      { 4   ;2   ;Field     ;
                  CaptionML=[ENU=Replace Posting Date;
                             FRA=Remplacer date comptabilisation];
                  SourceExpr=ReplacePostingDate;
                  OnValidate=BEGIN
                               IF ReplacePostingDate THEN
                                 MESSAGE(Text003);
                             END;
                              }

      { 5   ;2   ;Field     ;
                  CaptionML=[ENU=Replace Document Date;
                             FRA=Remplacer date document];
                  SourceExpr=ReplaceDocumentDate }

      { 7   ;2   ;Field     ;
                  CaptionML=[ENU=Calc. Inv. Discount;
                             FRA=Calculer remise facture];
                  SourceExpr=CalcInvDisc;
                  OnValidate=BEGIN
                               SalesSetup.GET;
                               SalesSetup.TESTFIELD("Calc. Inv. Discount",FALSE);
                             END;
                              }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Enter the posting date.;FRA=Entrez une date comptabilisation.';
      Text001@1001 : TextConst 'ENU=Posting orders  #1########## @2@@@@@@@@@@@@@;FRA=Validation des commandes #1########## @2@@@@@@@@@@@@@';
      Text002@1002 : TextConst 'ENU=%1 orders out of a total of %2 have now been posted.;FRA=%1 commande(s) sur %2 enregistr‚e(s).';
      Text003@1003 : TextConst 'ENU=The exchange rate associated with the new posting date on the sales header will not apply to the sales lines.;FRA=Le taux de change associ‚ … la nouvelle date de comptabilisation de l''en-tˆte vente ne s''appliquera pas aux lignes vente.';
      SalesLine@1005 : Record 37;
      SalesSetup@1006 : Record 311;
      SalesCalcDisc@1007 : Codeunit 60;
      SalesPost@1008 : Codeunit 80;
      Window@1009 : Dialog;
      ShipReq@1010 : Boolean;
      InvReq@1011 : Boolean;
      PostingDateReq@1012 : Date;
      CounterTotal@1013 : Integer;
      Counter@1014 : Integer;
      CounterOK@1015 : Integer;
      ReplacePostingDate@1016 : Boolean;
      ReplaceDocumentDate@1017 : Boolean;
      CalcInvDisc@1018 : Boolean;
      SendShipNotif@1000000002 : Boolean;
      InclureExpedPartielle@1000000001 : Boolean;
      ShowEndMsg@1000000000 : Boolean;

    LOCAL PROCEDURE CalculateInvoiceDiscount@1();
    BEGIN
      SalesLine.RESET;
      SalesLine.SETRANGE("Document Type","Sales Header"."Document Type");
      SalesLine.SETRANGE("Document No.","Sales Header"."No.");
      IF SalesLine.FINDFIRST THEN
        IF SalesCalcDisc.RUN(SalesLine) THEN BEGIN
          "Sales Header".GET("Sales Header"."Document Type","Sales Header"."No.");
          COMMIT;
        END;
    END;

    PROCEDURE InitializeRequest@2(ShipParam@1000 : Boolean;InvoiceParam@1001 : Boolean;PostingDateParam@1002 : Date;ReplacePostingDateParam@1003 : Boolean;ReplaceDocumentDateParam@1004 : Boolean;CalcInvDiscParam@1005 : Boolean);
    BEGIN
      ShipReq := ShipParam;
      InvReq := InvoiceParam;
      PostingDateReq := PostingDateParam;
      ReplacePostingDate := ReplacePostingDateParam;
      ReplaceDocumentDate := ReplaceDocumentDateParam;
      CalcInvDisc := CalcInvDiscParam;
    END;

    PROCEDURE InitReport@1180250001(ForShipReq@1180250001 : Boolean;ForInvReq@1180250002 : Boolean;ForPostingDateReq@1180250003 : Date;ForReplacePostingDate@1180250004 : Boolean;ForReplaceDocumentDate@1180250005 : Boolean;ForCalcInvDisc@1180250006 : Boolean;ForSendShipNotif@1180250007 : Boolean;ForInclureExpedPartielle@1180250008 : Boolean;ForShowEndMsg@1180250009 : Boolean);
    BEGIN
      //AC1.00 BEGIN
      ShipReq := ForShipReq;
      InvReq := ForInvReq;
      PostingDateReq := ForPostingDateReq;
      ReplacePostingDate := ForReplacePostingDate;
      ReplaceDocumentDate := ForReplaceDocumentDate;
      CalcInvDisc := ForCalcInvDisc;
      SendShipNotif := ForSendShipNotif;
      InclureExpedPartielle := ForInclureExpedPartielle;
      ShowEndMsg := ForShowEndMsg;
      //AC1.00 END
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

