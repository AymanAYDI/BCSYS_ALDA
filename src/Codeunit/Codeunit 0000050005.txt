OBJECT Codeunit 50005 B2B
{
  OBJECT-PROPERTIES
  {
    Date=10/03/14;
    Time=17:16:43;
    Modified=Yes;
    Version List=AC1.00;
  }
  PROPERTIES
  {
    TableNo=50001;
    OnRun=VAR
            CurrFile@1180250004 : File;
            FileInStream@1180250003 : InStream;
            Line@1180250006 : BigText;
          BEGIN
            EDISetup.GET("EDI Code");
            EDISetup.TESTFIELD("Sales Order Nos");

            IF GUIALLOWED THEN
              Window.OPEN(Text000);

            CheckDataError();
            IF "Data Error" = FALSE THEN BEGIN
              TESTFIELD(Mode,Mode::Normal);
              //N¯ sq
              EDIEntry.RESET;
              IF EDIEntry.FINDLAST THEN
                EntryNo := EDIEntry."Entry No."
              ELSE
                EntryNo := 0;
              //Enregistrement des lignes du fichier dans une table
              CLEAR(CurrFile);
              CurrFile.WRITEMODE := FALSE;
              CurrFile.TEXTMODE := TRUE;
              CurrFile.OPEN(EDISetup."File Imp. Path (Err.)" +'\' + Filename);
              CLEAR(FileInStream);
              CLEAR(Line);
              CLEAR(Fields_ToImport);
              CurrFile.CREATEINSTREAM(FileInStream);
              Line.READ(FileInStream);
              DecodeVariableFile(Line,Fields_ToImport, ';',"EDI Code","Treatment Date");
              CurrFile.CLOSE;
              //Transfert des enreg de la table vers les tables de vente
              RegisterRec(EDISetup."Target No.","EDI Code","Treatment Date");
            END ELSE BEGIN
              TESTFIELD(Mode,Mode::Recovery);
              //Transfert des enreg de la table vers les tables de vente
              RegisterRec(EDISetup."Target No.","EDI Code","Treatment Date");
              Mode := Mode::Normal;
              MODIFY;
            END;

            IF GUIALLOWED THEN
              Window.CLOSE;
          END;

  }
  CODE
  {
    VAR
      Text000@1180250011 : TextConst 'ENU=Batch in process...;FRA=Traitement en cours...';
      Text001@1180250002 : TextConst 'FRA=N¯ sÇquence %1 : %2';
      Text002@1180250003 : TextConst 'FRA=ERREUR! RÇf. non trouvÇe (cf. "DÇsignation 2").';
      EDISetup@1180250001 : Record 50000;
      EDIEntry@1180250016 : Record 50002;
      AsciiStr@1180250007 : Text[250];
      AnsiStr@1180250006 : Text[250];
      CharVar@1180250005 : ARRAY [32] OF Char;
      Text003@1180250009 : TextConst 'FRA="%1 n¯%2, %3=%4."';
      Text004@1180250010 : TextConst 'FRA=Nb. de ligne traitÇe : %1/%2.';
      Window@1180250008 : Dialog;
      FileInStream@1180250013 : InStream;
      Line@1180250012 : BigText;
      Fields_ToImport@1180250004 : ARRAY [100] OF Text[250];
      LineNo@1180250014 : Integer;
      EntryNo@1180250015 : Integer;

    PROCEDURE DecodeVariableFile@1000000000(_ReadLine@1000000000 : BigText;VAR _ResultFields@1000000005 : ARRAY [100] OF Text[250];_Separator@1000000009 : Text[3];EDICode@1180250006 : Code[10];EDILogDT@1180250005 : DateTime);
    VAR
      CR@1180250004 : Char;
      LF@1180250003 : Char;
      l_LineToRead@1000000001 : BigText;
      l_CurrPos@1000000002 : Integer;
      l_ReadField@1000000003 : Text[30];
      l_CurrFieldNo@1000000004 : Integer;
      l_LineToReadBuff@1000000006 : BigText;
      l_EndOfLine@1000000007 : Integer;
      l_EOL@1000000008 : Boolean;
      l_SalesLine@1180250001 : Record 37;
      l_Item@1180250002 : Record 27;
      AdjustedLength@1180250007 : Integer;
    BEGIN
      CR := 13;
      LF := 10;

      l_LineToRead := _ReadLine;
      CLEAR(_ResultFields);

      l_CurrFieldNo := 0;

      REPEAT
        //Nouvelle ligne
        IF l_EOL THEN BEGIN
          CLEAR(_ResultFields);
          l_CurrFieldNo := 0;
        END;

        l_CurrFieldNo += 1;
        l_EOL := FALSE;
        l_CurrPos := l_LineToRead.TEXTPOS(_Separator); //Position du prochain sÇparateur
        l_EndOfLine := l_LineToRead.TEXTPOS(FORMAT(CR)+FORMAT(LF)); //Position de la fin de ligne

        //Dernier champ de la ligne
        IF (l_CurrPos > l_EndOfLine) OR (l_CurrPos = 0) THEN BEGIN
          l_EOL := TRUE;
          l_CurrPos := l_EndOfLine;
        END;

        IF l_CurrPos <> 0 THEN BEGIN
          //Alimentation du tableau result
          IF l_CurrPos-1 > 250 THEN
            AdjustedLength := 250
          ELSE
            AdjustedLength := l_CurrPos-1;
          l_LineToRead.GETSUBTEXT(_ResultFields[l_CurrFieldNo],1,AdjustedLength);
          //Suppression des donnÇes lues
          CLEAR(l_LineToReadBuff);
          l_LineToReadBuff.ADDTEXT(l_LineToRead);
          CLEAR(l_LineToRead);
          IF NOT l_EOL THEN
            l_LineToReadBuff.GETSUBTEXT(l_LineToRead, l_CurrPos+1)
          ELSE BEGIN
            l_LineToReadBuff.GETSUBTEXT(l_LineToRead, l_CurrPos+2);
            //InterprÇtation de la ligne
            TreatLine(Fields_ToImport,EDICode,EDILogDT);
            LineNo += 1;
          END;
        END;
      UNTIL l_CurrPos = 0;
    END;

    PROCEDURE TreatLine@1180250003(_ResultFields@1180210000 : ARRAY [100] OF Text[250];EDICode@1180250003 : Code[10];EDILogDT@1180250002 : DateTime);
    VAR
      i@1180250004 : Integer;
    BEGIN
      FOR i := 1 TO 67 DO
        _ResultFields[i] := Ascii2Ansi(_ResultFields[i]);

      IF _ResultFields[1] <> '' THEN BEGIN
        EntryNo += 1;

        WITH EDIEntry DO BEGIN
          INIT;
          "Entry No." := EntryNo;
          "User ID" := USERID;
          "Imp./Exp. Date" := EDILogDT;
          "EDI Code" := EDICode;
          Error := TRUE;
          INSERT;

          "Field 1" := COPYSTR(_ResultFields[1],1,50);//Code client facturÇ
          "Field 2" := COPYSTR(_ResultFields[2],1,50);//Code client livrÇ
          "Field 3" := COPYSTR(_ResultFields[3],1,50);//Code commande
          "Field 4" := COPYSTR(_ResultFields[4],1,50);//N¯ commande interne
          "Field 5" := COPYSTR(_ResultFields[5],1,50);//Date d'envoie
          "Field 6" := COPYSTR(_ResultFields[6],1,50);//N¯ de ligne
          "Field 7" := COPYSTR(_ResultFields[7],1,50);//RÇfÇrence article
          "Field 8" := COPYSTR(_ResultFields[8],1,50);//TVA
          "Field 9" := COPYSTR(_ResultFields[9],1,50);//DÇsigntaion
          "Field 10" := COPYSTR(_ResultFields[10],1,50);//Paquetage
          "Field 11" := COPYSTR(_ResultFields[11],1,50);//UnitÇ de facturation
          "Field 12" := COPYSTR(_ResultFields[12],1,50);//PU
          "Field 13" := COPYSTR(_ResultFields[13],1,50);//QtÇ
          "Field 14" := COPYSTR(_ResultFields[14],1,50);//HT
          "Field 15" := COPYSTR(_ResultFields[15],1,50);//TTC
          "Field 16" := COPYSTR(_ResultFields[16],1,50);//Date de livraison demandÇe
          "Field 17" := COPYSTR(_ResultFields[17],1,50);//Commentaire
          "Field 18" := COPYSTR(_ResultFields[17],51,50);
          "Field 19" := COPYSTR(_ResultFields[17],101,50);
          "Field 20" := COPYSTR(_ResultFields[17],151,50);
          "Field 21" := COPYSTR(_ResultFields[17],201,50);
          "Field 22" := COPYSTR(_ResultFields[17],251);
          //"Field 23" := COPYSTR(_ResultFields[17],301,50); Peu utilisÇ donc supprimÇ
          //"Field 24" := COPYSTR(_ResultFields[17],351,50); Peu utilisÇ donc supprimÇ
          //"Field 25" := COPYSTR(_ResultFields[17],401,50); Peu utilisÇ donc supprimÇ
          //"Field 26" := COPYSTR(_ResultFields[17],451,50); Peu utilisÇ donc supprimÇ
          "Field 27" := COPYSTR(_ResultFields[18],1,50);//Montant du port
          "Field 28" := COPYSTR(_ResultFields[19],1,50);//Champ libre
          "Field 29" := COPYSTR(_ResultFields[20],1,50);//Type origine commande
          MODIFY;
        END;//WITH
      END;
    END;

    LOCAL PROCEDURE RegisterRec@1180210000(VendorNo@1180250004 : Code[20];EDICode@1180250008 : Code[10];EDILogDT@1180250006 : DateTime);
    VAR
      CompanyInfo@1180250012 : Record 79;
      GLSetup@1180250001 : Record 98;
      SalesSetup@1180250015 : Record 311;
      Customer@1180250021 : Record 18;
      SalesHeader@1180250009 : Record 36;
      SalesLine@1180250003 : Record 37;
      SalesCommentLine@1180250002 : Record 44;
      TmpEDIEntry2@1180250007 : TEMPORARY Record 50002;
      ItemNo@1180250018 : Code[20];
      ItemCrossRefNo@1180250019 : Code[20];
      VendorNo2@1180250020 : Code[20];
      CurrItemNoIDPrefix@1180250016 : Text[30];
      LineNo@1180250010 : Integer;
      LineNo2@1180250011 : Integer;
      LenItemNoIDPrefix@1180250017 : Integer;
      ItemChargeAmt@1180250005 : Decimal;
      NbrOfLine@1180250023 : Integer;
      NbrOfHandledLine@1180250022 : Integer;
      i@1180250013 : Integer;
      ReqDeliveryDate@1180250014 : Date;
    BEGIN
      EDISetup.GET(EDICode);
      GLSetup.GET();
      CompanyInfo.GET();
      SalesSetup.GET();

      CLEAR(TmpEDIEntry2);
      TmpEDIEntry2.RESET;
      TmpEDIEntry2.DELETEALL;

      NbrOfHandledLine := 0;
      NbrOfLine := 0;

      IF NOT SalesHeader.RECORDLEVELLOCKING THEN
        SalesHeader.LOCKTABLE(TRUE,TRUE);
      IF NOT SalesLine.RECORDLEVELLOCKING THEN
        SalesLine.LOCKTABLE(TRUE,TRUE);

      EDIEntry.RESET;
      EDIEntry.SETRANGE("EDI Code",EDICode);
      EDIEntry.SETRANGE("Imp./Exp. Date",EDILogDT);
      IF EDIEntry.FINDSET(FALSE,FALSE) THEN BEGIN
        NbrOfLine := EDIEntry.COUNT;
        REPEAT
          TmpEDIEntry2 := EDIEntry;
          IF TmpEDIEntry2.INSERT THEN;
          //Tempo
          IF EDISetup."Control of Time (milliseconds)" <> 0 THEN
            SLEEP(EDISetup."Control of Time (milliseconds)");
          //En-tàte (toujours la 1er ligne du fichier)
          IF NbrOfHandledLine  = 0 THEN BEGIN
            Customer.GET(EDIEntry."Field 2");
            Customer.CALCFIELDS("Dim. Shipping Method Code");

            ReqDeliveryDate := ConvText2Date(EDIEntry."Field 16");

            SalesHeader.INIT;
            SalesHeader."Document Type" := SalesHeader."Document Type"::Order;
            SalesHeader."No. Series" := EDISetup."Sales Order Nos";
            SalesHeader.INSERT(TRUE);
            SalesHeader."Last EDI Code for Batch" := EDICode;
            SalesHeader."Last EDI DT for Batch" := EDILogDT;
            SalesHeader."Order Type" := SalesHeader."Order Type"::DD;
            SalesHeader.VALIDATE("Sell-to Customer No.",EDIEntry."Field 2");
            SalesHeader.VALIDATE("Requested Delivery Date",ReqDeliveryDate);
            IF EDIEntry."Field 1" <> EDIEntry."Field 2" THEN
              SalesHeader.VALIDATE("Bill-to Customer No.",EDIEntry."Field 1");
            //EDIEntry."Field 3" : Code commande, c'est dire le n¯ de cde du B2B, existe toujours
            SalesHeader."Your Reference" := COPYSTR(EDIEntry."Field 3",1,20);
            //EDIEntry."Field 4" : N¯ commande interne, n¯ de cde du client, n'existe pas toujours mais c'est celui qui doit àtre pris
            IF EDIEntry."Field 4" <> '' THEN BEGIN
              SalesHeader."External Document No." := COPYSTR(EDIEntry."Field 4",1,20);
            END ELSE BEGIN
              SalesHeader."External Document No." := COPYSTR(EDIEntry."Field 3",1,20);
            END;
            IF EDIEntry."Field 5" <> '' THEN
              SalesHeader.VALIDATE("Order Date",ConvText2Date(EDIEntry."Field 5"))
            ELSE
              SalesHeader.VALIDATE("Order Date",TODAY);
            IF SalesHeader."Bill-to Customer No." <> SalesHeader."Sell-to Customer No." THEN
              Customer.GET(SalesHeader."Bill-to Customer No.")
            ELSE
              Customer.GET(SalesHeader."Sell-to Customer No.");
            Customer.CALCFIELDS("Dim. Shipping Method Code");
            CASE Customer."Dim. Shipping Method Code" OF
              'DCL':SalesHeader."Order Type" := SalesHeader."Order Type"::DD;
              'DG':SalesHeader."Order Type" := SalesHeader."Order Type"::DG;
              ELSE
                SalesHeader."Order Type" := SalesHeader."Order Type"::AA;
            END;//CASE
            //Type d'origine
            CASE TRUE OF
              EDIEntry."Field 29" IN ['mail','Mail','MAIL','e-mail','E-MAIL']:
                SalesHeader."Order Source Type" := SalesHeader."Order Source Type"::Mail;
              EDIEntry."Field 29" IN ['fax','Fax','FAX','TÇlÇcopie','TELECOPIE']:
                SalesHeader."Order Source Type" := SalesHeader."Order Source Type"::Fax;
              EDIEntry."Field 29" IN ['tÇl','TÇl','tel','TEL','TÇlÇphone','TELEPHONE']:
                SalesHeader."Order Source Type" := SalesHeader."Order Source Type"::Phone;
              EDIEntry."Field 29" IN ['internet','Internet','INTERNET','web','Web','WEB','site','Site','SITE']:
                SalesHeader."Order Source Type" := SalesHeader."Order Source Type"::Web;
              EDIEntry."Field 29" IN ['Devis','DEVIS','devis']:
                SalesHeader."Order Source Type" := SalesHeader."Order Source Type"::Quote;
            END;//CASE
            SalesHeader.MODIFY(TRUE);
            SalesHeader.VALIDATE("External Document No.");
            SalesHeader.MODIFY;
            //Frais de port
            ItemChargeAmt := ConvTxt2Dec(EDIEntry."Field 27");
            //Commentaires
            LineNo2 := 0;
            IF EDIEntry."Field 17" <> '' THEN BEGIN
              LineNo2 += 10000;
              SalesCommentLine.INIT;
              SalesCommentLine."Document Type" := SalesHeader."Document Type";
              SalesCommentLine."No." := SalesHeader."No.";
              SalesCommentLine."Document Line No." := 0;
              SalesCommentLine."Line No." := LineNo2;
              SalesCommentLine.INSERT;
              SalesCommentLine.Date := TODAY;
              SalesCommentLine.Comment :=
              EDIEntry."Field 17";
              SalesCommentLine.MODIFY;
            END;
            IF EDIEntry."Field 18" <> '' THEN BEGIN
              LineNo2 += 10000;
              SalesCommentLine.INIT;
              SalesCommentLine."Document Type" := SalesHeader."Document Type";
              SalesCommentLine."No." := SalesHeader."No.";
              SalesCommentLine."Document Line No." := 0;
              SalesCommentLine."Line No." := LineNo2;
              SalesCommentLine.INSERT;
              SalesCommentLine.Date := TODAY;
              SalesCommentLine.Comment := EDIEntry."Field 18";
              SalesCommentLine.MODIFY;
            END;
            IF EDIEntry."Field 19" <> '' THEN BEGIN
              LineNo2 += 10000;
              SalesCommentLine.INIT;
              SalesCommentLine."Document Type" := SalesHeader."Document Type";
              SalesCommentLine."No." := SalesHeader."No.";
              SalesCommentLine."Document Line No." := 0;
              SalesCommentLine."Line No." := LineNo2;
              SalesCommentLine.INSERT;
              SalesCommentLine.Date := TODAY;
              SalesCommentLine.Comment := EDIEntry."Field 19";
              SalesCommentLine.MODIFY;
            END;
            IF EDIEntry."Field 20" <> '' THEN BEGIN
              LineNo2 += 10000;
              SalesCommentLine.INIT;
              SalesCommentLine."Document Type" := SalesHeader."Document Type";
              SalesCommentLine."No." := SalesHeader."No.";
              SalesCommentLine."Document Line No." := 0;
              SalesCommentLine."Line No." := LineNo2;
              SalesCommentLine.INSERT;
              SalesCommentLine.Date := TODAY;
              SalesCommentLine.Comment := EDIEntry."Field 20";
              SalesCommentLine.MODIFY;
            END;
            IF EDIEntry."Field 21" <> '' THEN BEGIN
              LineNo2 += 10000;
              SalesCommentLine.INIT;
              SalesCommentLine."Document Type" := SalesHeader."Document Type";
              SalesCommentLine."No." := SalesHeader."No.";
              SalesCommentLine."Document Line No." := 0;
              SalesCommentLine."Line No." := LineNo2;
              SalesCommentLine.INSERT;
              SalesCommentLine.Date := TODAY;
              SalesCommentLine.Comment := EDIEntry."Field 21";
              SalesCommentLine.MODIFY;
            END;
            IF EDIEntry."Field 22" <> '' THEN BEGIN
              LineNo2 += 10000;
              SalesCommentLine.INIT;
              SalesCommentLine."Document Type" := SalesHeader."Document Type";
              SalesCommentLine."No." := SalesHeader."No.";
              SalesCommentLine."Document Line No." := 0;
              SalesCommentLine."Line No." := LineNo2;
              SalesCommentLine.INSERT;
              SalesCommentLine.Date := TODAY;
              SalesCommentLine.Comment := EDIEntry."Field 22";
              SalesCommentLine.MODIFY;
            END;
            //Le fichier d'origine permet une taille de commentaire au max de 500 caractäres (EDIEntry."Field 23"..EDIEntry."Field 26" qui
            //ÇtÇ supprimer... C'est pourquoi on redÇmarra au champ EDIEntry."Field 27" pour le frais de port
          END;
          //Recherche de l'article
          IF EDISetup."Item No. Itentification Prefix" = '' THEN BEGIN
            ItemNo := GetItemFromItemCrossRefVendor(VendorNo,EDIEntry."Field 7");
            ItemCrossRefNo := FORMAT(EDIEntry."Field 7");
          END ELSE BEGIN
            LenItemNoIDPrefix := STRLEN(EDISetup."Item No. Itentification Prefix");
            CurrItemNoIDPrefix := COPYSTR(EDIEntry."Field 7",1,LenItemNoIDPrefix);
            IF CurrItemNoIDPrefix = EDISetup."Item No. Itentification Prefix" THEN BEGIN
              ItemNo := FORMAT(COPYSTR(EDIEntry."Field 7",LenItemNoIDPrefix + 1,STRLEN(EDIEntry."Field 7")));
              ItemCrossRefNo := COPYSTR(EDIEntry."Field 7",LenItemNoIDPrefix + 1);
              VendorNo2 := '';
            END ELSE BEGIN
              ItemNo := GetItemFromItemCrossRefVendor(VendorNo,EDIEntry."Field 7");
              ItemCrossRefNo := FORMAT(EDIEntry."Field 7");
              VendorNo2 := VendorNo;
            END;
          END;
          //CrÇation de la ligne
          LineNo += 10000;
          SalesLine.INIT;
          SalesLine.SetHideValidationDialog(EDISetup."Hide Validation Dialog");
          SalesLine."Document Type" := SalesHeader."Document Type";
          SalesLine."Document No." := SalesHeader."No.";
          SalesLine."Line No." := LineNo;
          SalesLine.INSERT(TRUE);

          NbrOfHandledLine += 1;

          IF ItemNo <> '' THEN BEGIN
            SalesLine.Type := SalesLine.Type::Item;
            SalesLine.VALIDATE("No.",ItemNo);
            SalesLine.VALIDATE(Quantity,ConvTxt2Dec(EDIEntry."Field 13"));
            SalesLine.VALIDATE("Requested Delivery Date",ReqDeliveryDate);
            SalesLine.VALIDATE("Unit Price",ConvTxt2Dec(EDIEntry."Field 12"));
            SalesLine."Order Reference Customer" := SalesHeader."Order Reference Customer";
            SalesLine."Line No. Reference Customer" := SalesLine."Line No.";
            IF SalesHeader."Order Type" <> SalesHeader."Order Type"::AA THEN BEGIN
              SalesLine."Vendor No. for EDI Sending" := VendorNo2;
              SalesLine."Cross-Reference No. of Vendor" := ItemCrossRefNo;
              SalesLine.VALIDATE("Qty. to Ship",0);
            END;
            SalesLine.MODIFY(TRUE);
          END ELSE BEGIN
            SalesLine.Type := SalesLine.Type::" ";
            SalesLine.Description := Text002;
            SalesLine."Description 2":= EDIEntry."Field 7";
            SalesLine."Order Reference Customer" := SalesHeader."Order Reference Customer";
            SalesLine.MODIFY(TRUE);
            CreateEDILogEntry(EDICode,EDILogDT,STRSUBSTNO(Text003,SalesLine."Document Type",SalesLine."Document No.",
              SalesLine.FIELDCAPTION("Line No."),SalesLine."Line No."),2,EDIEntry."Entry No.");
          END;
          //Tag si ok
          TmpEDIEntry2.Error := FALSE;
          TmpEDIEntry2.MODIFY;

        UNTIL EDIEntry.NEXT = 0;
      END;

      IF NbrOfHandledLine = NbrOfLine THEN
        CreateEDILogEntry(EDICode,EDILogDT,STRSUBSTNO(Text004,NbrOfHandledLine,NbrOfLine),1,0)
      ELSE
        CreateEDILogEntry(EDICode,EDILogDT,STRSUBSTNO(Text004,NbrOfHandledLine,NbrOfLine),2,0);

      IF ItemChargeAmt <> 0 THEN BEGIN
        LineNo += 10000;
        SalesLine.INIT;
        SalesLine."Document Type" := SalesHeader."Document Type";
        SalesLine."Document No." := SalesHeader."No.";
        SalesLine."Line No." := LineNo;
        SalesLine.INSERT(TRUE);
        SalesLine.Type := SalesLine.Type::Item;
        SalesLine.VALIDATE("No.",SalesSetup."Item No. for Shipping Charge");
        SalesLine.VALIDATE(Quantity,1);
        SalesLine.VALIDATE("Unit Price",ItemChargeAmt);
        SalesLine."Order Reference Customer" := SalesHeader."Order Reference Customer";
        SalesLine.MODIFY;
        SalesLine.VALIDATE("Vendor No. for EDI Sending",'');
        SalesLine.MODIFY(TRUE);
      END;

      TmpEDIEntry2.RESET;
      TmpEDIEntry2.SETRANGE(Error,FALSE);
      IF TmpEDIEntry2.FINDSET(FALSE,FALSE) THEN
        REPEAT
          EDIEntry := TmpEDIEntry2;
          EDIEntry.Handled := TRUE;
          EDIEntry.MODIFY;
        UNTIL TmpEDIEntry2.NEXT = 0;
    END;

    LOCAL PROCEDURE ConvTxt2Dec@1180250002(Txt2Conv@1180250001 : Text[50]) DecConv : Decimal;
    BEGIN
      IF Txt2Conv <> '' THEN
        EVALUATE(DecConv,CONVERTSTR(Txt2Conv,'.',','))
      ELSE
        DecConv := 0;
    END;

    LOCAL PROCEDURE ConvTxt2Int@1180250001(Txt2Conv@1180250001 : Text[50]) IntConv : Integer;
    BEGIN
      IF Txt2Conv <> '' THEN
        EVALUATE(IntConv,Txt2Conv)
      ELSE
        IntConv := 0;
    END;

    PROCEDURE ConvText2Date@1180250010(Txt2Conv@1180250001 : Text[50]) OutPutDate : Date;
    VAR
      DayInInt@1180250003 : Integer;
      MonthinInt@1180250002 : Integer;
      YearInInt@1180250004 : Integer;
    BEGIN
      IF Txt2Conv <> '' THEN BEGIN
        EVALUATE(DayInInt,COPYSTR(Txt2Conv,1,2));
        EVALUATE(MonthinInt,COPYSTR(Txt2Conv,4,2));
        EVALUATE(YearInInt,COPYSTR(Txt2Conv,7,4));

        OutPutDate := DMY2DATE(DayInInt,MonthinInt,YearInInt);
      END ELSE
        OutPutDate := 0D;
    END;

    PROCEDURE Ansi2Ascii@1180250005(_Text@1180250001 : Text[250]) : Text[250];
    BEGIN
      MakeVars;
      EXIT(CONVERTSTR(_Text,AnsiStr,AsciiStr));
    END;

    PROCEDURE Ascii2Ansi@1180250007(_Text@1180250001 : Text[250]) : Text[250];
    BEGIN
      MakeVars;
      EXIT(CONVERTSTR(_Text,AsciiStr,AnsiStr));
    END;

    PROCEDURE MakeVars@1180250006();
    BEGIN
      AsciiStr := 'ÄÅÇÉÑÖÜáàâäãåçéèêëíìîïñóòôöõúùûü†°¢£§•¶ß®©™´¨≠ÆØ›››››µ∂∑∏››++Ωæ++--+-+∆«++--›-+';
      AsciiStr := AsciiStr +'œ–—“”‘i÷◊ÿ++›_›ﬁÓ‡·‚„‰ÂÊÁËÈÍÎÏÌÓÔÒ=ÛÙıˆ˜¯˘˙˚¸˝›ˇ';
      CharVar[1] := 196;
      CharVar[2] := 197;
      CharVar[3] := 201;
      CharVar[4] := 242;
      CharVar[5] := 220;
      CharVar[6] := 186;
      CharVar[7] := 191;
      CharVar[8] := 188;
      CharVar[9] := 187;
      CharVar[10] := 193;
      CharVar[11] := 194;
      CharVar[12] := 192;
      CharVar[13] := 195;
      CharVar[14] := 202;
      CharVar[15] := 203;
      CharVar[16] := 200;
      CharVar[17] := 205;
      CharVar[18] := 206;
      CharVar[19] := 204;
      CharVar[20] := 175;
      CharVar[21] := 223;
      CharVar[22] := 213;
      CharVar[23] := 254;
      CharVar[24] := 218;
      CharVar[25] := 219;
      CharVar[26] := 217;
      CharVar[27] := 180;
      CharVar[28] := 177;
      CharVar[29] := 176;
      CharVar[30] := 185;
      CharVar[31] := 179;
      CharVar[32] := 178;
      AnsiStr  := '«¸È‚‰‡ÂÁÍÎËÔÓÏ'+FORMAT(CharVar[1])+FORMAT(CharVar[2])+FORMAT(CharVar[3])+ 'Ê∆Ùˆ'+FORMAT(CharVar[4]);
      AnsiStr := AnsiStr + '˚˘ˇ÷'+FORMAT(CharVar[5])+'¯£ÿ◊É·ÌÛ˙Ò—™'+FORMAT(CharVar[6])+FORMAT(CharVar[7]);
      AnsiStr := AnsiStr + 'Æ¨Ω'+FORMAT(CharVar[8])+'°´'+FORMAT(CharVar[9])+'___¶¶' + FORMAT(CharVar[10])+FORMAT(CharVar[11]);
      AnsiStr := AnsiStr + FORMAT(CharVar[12]) + '©¶¶++¢•++--+-+„' + FORMAT(CharVar[13]) + '++--¶-+§–';
      AnsiStr  :=  AnsiStr +FORMAT(CharVar[14])+FORMAT(CharVar[15])+FORMAT(CharVar[16])+'i'+FORMAT(CharVar[17])+FORMAT(CharVar[18]);
      AnsiStr  :=  AnsiStr + 'œ++__¶' + FORMAT(CharVar[19])+FORMAT(CharVar[20])+'”'+FORMAT(CharVar[21])+'‘“ı';
      AnsiStr  :=  AnsiStr + FORMAT(CharVar[22]) + 'µ' + FORMAT(CharVar[23]) + 'ﬁ' + FORMAT(CharVar[24])+ FORMAT(CharVar[25]);
      AnsiStr  :=  AnsiStr + FORMAT(CharVar[26]) + '˝›Ø' + FORMAT(CharVar[27]) + '≠' + FORMAT(CharVar[28]) +'=æ∂ß˜∏'+ FORMAT(CharVar[29]
      );
      AnsiStr  :=  AnsiStr + '®∑' + FORMAT(CharVar[30]) +FORMAT(CharVar[31]) +FORMAT(CharVar[32]) +'_ ';
    END;

    PROCEDURE GetItemFromItemCrossRefVendor@1180250008(VendorNo@1180250001 : Code[20];CrossRefNo@1180250003 : Code[20]) : Code[20];
    VAR
      ItemCrossReference@1180250002 : Record 5717;
    BEGIN
      ItemCrossReference.RESET;
      ItemCrossReference.SETRANGE("Cross-Reference Type",ItemCrossReference."Cross-Reference Type"::Vendor);
      ItemCrossReference.SETRANGE("Cross-Reference Type No.",VendorNo);
      ItemCrossReference.SETRANGE("Cross-Reference No.",CrossRefNo);
      IF ItemCrossReference.FINDFIRST THEN
        EXIT(ItemCrossReference."Item No.")
      ELSE
        EXIT('');
    END;

    PROCEDURE GetCust@1180250009(CustName@1180250001 : Text[50]) : Code[20];
    VAR
      Cust@1180250002 : Record 18;
    BEGIN
      Cust.RESET;
      Cust.SETFILTER(Name,'%1',CustName);
      Cust.FINDFIRST;
      EXIT(Cust."No.")
    END;

    PROCEDURE CreateEDILogEntry@1180250011(EDICode@1180250003 : Code[10];EDILogDT@1180250002 : DateTime;String@1180250001 : Text[250];Status@1180250006 : ' ,OK,Error';EntryNo2@1180250007 : Integer);
    VAR
      EDILogEntry@1180250004 : Record 50004;
      LineNo@1180250005 : Integer;
    BEGIN
      EDILogEntry.RESET;
      EDILogEntry.SETRANGE("EDI Code",EDICode);
      EDILogEntry.SETRANGE("Treatment Date",EDILogDT);
      IF EDILogEntry.FINDLAST THEN
        LineNo := EDILogEntry."Ligne No."
      ELSE
        LineNo := 0;

      LineNo += 10000;

      EDILogEntry.INIT;
      EDILogEntry."EDI Code" := EDICode;
      EDILogEntry."Treatment Date" := EDILogDT;
      EDILogEntry."Ligne No." := LineNo;
      EDILogEntry."Error Text" := String;
      EDILogEntry.Status := Status;
      EDILogEntry."Applied E.D.I. Entry No." := EntryNo2;
      EDILogEntry.INSERT;
    END;

    BEGIN
    END.
  }
}

