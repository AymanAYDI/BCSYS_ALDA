OBJECT Codeunit 50016 DF Sales Invoice by Cust.
{
  OBJECT-PROPERTIES
  {
    Date=19/05/16;
    Time=17:24:27;
    Modified=Yes;
    Version List=BC6AUTOINV;
  }
  PROPERTIES
  {
    TableNo=50018;
    OnRun=VAR
            DataFlowMgt@1000000000 : Codeunit 50013;
            Cust@1000000001 : Record 18;
            TempSalesShptLine@1000000003 : TEMPORARY Record 111;
            SalesHeader@1000000004 : Record 36;
            SalesLine@1000000005 : Record 37;
            CounterOK@1000000002 : Integer;
          BEGIN
            TESTFIELD("Source Table ID", DATABASE::Customer);
            TESTFIELD("Data Flow Code");
            DataFlowMgt.SetDataFlowStep(Rec);

            Cust.SETVIEW(GetViewRestrictions());
            SalesHeader.SetHideValidationDialog(TRUE);
            SalesLine.SetHideValidationDialog(TRUE);
            StateIndicator.Open();
            StateIndicator.Start();
            StateIndicator.UpdateDataFlowStep(Rec);
            StateIndicator.InsertLogEntry(Rec);
            IF Cust.FINDSET THEN
              REPEAT
                StateIndicator.Update();
                IF DataFlowMgt.FindSalesShptLines(TempSalesShptLine, Cust, StateIndicator) THEN BEGIN
                  SalesHeader.INIT;
                  SalesHeader."No." := '';
                  SalesHeader."Document Type" := SalesHeader."Document Type"::Invoice;
                  SalesHeader.INSERT(TRUE);

                  SalesHeader.VALIDATE("Sell-to Customer No.", Cust."No.");
                  DataFlowMgt.StampSalesHeader(SalesHeader, Rec);
                  SalesHeader.MODIFY(TRUE);
                  DataFlowMgt.CreateInvLines(TempSalesShptLine, SalesLine, SalesHeader, StateIndicator);
                  SalesHeader.MODIFY(TRUE);
                  CounterOK := CounterOK + 1;
                END;
              UNTIL Cust.NEXT = 0;
            StateIndicator.EndUpdateDataFlowStep(Rec);
            StateIndicator.UpdateLogEntry(Rec, STRSUBSTNO(Text002, CounterOK, Cust.COUNT));
          END;

  }
  CODE
  {
    VAR
      StateIndicator@1000000000 : TEMPORARY Record 50005;
      Text002@1000000002 : TextConst 'ENU=%1 invoices out of a total of %2 customers have been created.;FRA=%1 facture(s) ont ‚t‚ cr‚e(s) pour %2 Client(s).';

    BEGIN
    END.
  }
}

